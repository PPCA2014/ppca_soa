%%********************************************************************
%% @title Module ems_user_telefone_loader_middleware
%% @version 1.0.0
%% @doc Module responsible for load telefone
%% @author Everton de Vargas Agilar <evertonagilar@gmail.com>
%% @copyright ErlangMS Team
%%********************************************************************

-module(ems_user_telefone_loader_middleware).

-include("include/ems_config.hrl").
-include("include/ems_schema.hrl").

-export([insert_or_update/5, is_empty/1, size_table/1, clear_table/1, reset_sequence/1, get_filename/0, check_remove_records/2]).


-spec is_empty(fs | db) -> boolean().
is_empty(db) ->	mnesia:table_info(user_telefone_db, size) == 0;
is_empty(fs) ->	mnesia:table_info(user_telefone_fs, size) == 0.
	

-spec size_table(fs | db) -> non_neg_integer().
size_table(db) -> mnesia:table_info(user_telefone_db, size);
size_table(fs) -> mnesia:table_info(user_telefone_fs, size).
	

-spec clear_table(fs | db) -> ok | {error, efail_clear_ets_table}.
clear_table(db) ->	
	case mnesia:clear_table(user_telefone_db) of
		{atomic, ok} -> ok;
		_ -> {error, efail_clear_ets_table}
	end;
clear_table(fs) ->	
	case mnesia:clear_table(user_telefone_fs) of
		{atomic, ok} -> ok;
		_ -> {error, efail_clear_ets_table}
	end.
	
	
-spec reset_sequence(fs | db) -> ok.
reset_sequence(db) -> 
	ems_db:init_sequence(user_telefone_db, 0),
	ok;
reset_sequence(fs) ->	
	ems_db:init_sequence(user_telefone_fs, 0),
	ok.

-spec check_remove_records(list(), fs | db) -> non_neg_integer().	
check_remove_records(_Codigos, _SourceType) -> 0.
	

%% internal functions

-spec get_filename() -> list(tuple()).
get_filename() -> 
	Conf = ems_config:getConfig(),
	Conf#config.user_telefone_path_search.
	


update_telefone_tabela_users(Telefone = #user_telefone{type = Type}, SourceType, CodigoPessoa) ->
	if 
		Type == 1 orelse Type == 3 ->
			UserTable = ems_user:get_table(SourceType),
			case ems_user:find_by_codigo_pessoa(UserTable, CodigoPessoa) of
				{ok, Users} -> 
					update_telefone_tabela_users_(Users, Telefone, UserTable);
				_ -> ok
			end;
		true -> ok
	end.

-spec update_telefone_tabela_users_(list(#user{}), #user_telefone{}, atom()) -> ok.
update_telefone_tabela_users_([], _, _) -> ok;
update_telefone_tabela_users_([User|UserT], 
							 Telefone, 
							 UserTable) -> 
	case Telefone#user_telefone.type of
		1 -> %% celular
			User2 = User#user{celular = Telefone#user_telefone.numero};
		3 -> %% telefone residencial
			User2 = User#user{telefone = Telefone#user_telefone.numero}
	end,
	mnesia:dirty_write(UserTable, User2),
	update_telefone_tabela_users_(UserT, Telefone, UserTable).
		
	
-spec insert_or_update(map() | tuple(), tuple(), #config{}, atom(), insert | update) -> {ok, #service{}, atom(), insert | update} | {ok, skip} | {error, atom()}.
insert_or_update(Map, CtrlDate, Conf, SourceType, _Operation) ->
	try
		case ems_user_telefone:new_from_map(Map, Conf) of
			{ok, NewRecord = #user_telefone{id = Id, ctrl_hash = CtrlHash, codigo = CodigoPessoa}} -> 
				Table = ems_user_telefone:get_table(SourceType),
				case ems_user_telefone:find(Table, Id) of
					{error, enoent} -> 
						Record = NewRecord#user_telefone{ctrl_insert = CtrlDate},
						update_telefone_tabela_users(Record, case SourceType of
																db -> user_db;
																fs -> user_fs
															 end, CodigoPessoa),
						{ok, Record, Table, insert};
					{ok, CurrentRecord = #user_telefone{ctrl_hash = CurrentCtrlHash}} ->
						case CtrlHash =/= CurrentCtrlHash of
							true ->
								?DEBUG("ems_user_telefone_loader_middleware update ~p from ~p.", [Map, SourceType]),
								Record = CurrentRecord#user_telefone{
												 codigo = CodigoPessoa,
												 numero = NewRecord#user_telefone.numero,
												 ramal = NewRecord#user_telefone.ramal,
												 ddd = NewRecord#user_telefone.ddd,
												 type = NewRecord#user_telefone.type,
												 ctrl_path = NewRecord#user_telefone.ctrl_path,
												 ctrl_file = NewRecord#user_telefone.ctrl_file,
												 ctrl_update = CtrlDate,
												 ctrl_modified = NewRecord#user_telefone.ctrl_modified,
												 ctrl_hash = NewRecord#user_telefone.ctrl_hash
											},
								update_telefone_tabela_users(Record, case SourceType of
																db -> user_db;
																fs -> user_fs
														  end, CodigoPessoa),

								{ok, Record, Table, update};
							false -> {ok, skip}
						end
				end;
			Error -> Error
		end

	catch
		_Exception:Reason -> {error, Reason}
	end.


	


